group = 'dev.ivk'
archivesBaseName = "ivk-skill-sdk"
version =  file("$projectDir/version.txt").text.trim()

buildscript {
    ext.kotlin_version = '2.2.0'
    ext.spotless_version = "6.25.0"
    ext.dokka_version = "2.0.0"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotless_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"

    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.diffplug.spotless'


repositories {
    mavenCentral()
}

// Use spotless plugin to automatically format code, remove unused import, etc
// To apply changes directly to the file, run `gradlew spotlessApply`
// Ref: https://github.com/diffplug/spotless/tree/main/plugin-gradle
spotless {
    // comment out below to run spotless as part of the `check` task
    enforceCheck false

    format 'misc', {
        // define the files (e.g. '*.gradle', '*.md') to apply `misc` to
        target '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces() // Takes an integer argument if you don't like 4
        endWithNewline()
    }
    kotlin {
        ktfmt()
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.squareup.moshi:moshi-kotlin:1.15.1"
    implementation "com.squareup.moshi:moshi-adapters:1.15.1"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation("org.slf4j:slf4j-api:2.0.17")
    testImplementation "io.kotest:kotest-runner-junit5-jvm:6.0.7"
    testImplementation "io.kotest:kotest-assertions-core-jvm:6.0.7"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.1"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.12.0"
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}
tasks.register('javadocJar', Jar) {
    dependsOn 'dokkaJavadoc'
    from "$buildDir/dokka/javadoc"
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            from components.java
            artifact tasks.named('sourcesJar')
            artifact tasks.named('javadocJar')

            pom {
                name.set("IVK Skill SDK")
                description.set("Kotlin SDK for the IVK Skill API with coroutine, blocking, and CompletableFuture APIs.")
                url.set("https://github.com/invokation-games/kotlin-sdk")
                licenses {
                    license {
                        name.set("Apache License 2.0")
                        url.set("https://www.apache.org/licenses/LICENSE-2.0")
                        distribution.set("repo")
                    }
                }
                developers {
                    developer {
                        id.set("ivk")
                        name.set("Robin Van Cauter")
                        email.set("robin@invokation.games")
                        organization.set("Invokation")
                        organizationUrl.set("https://invokation.games")
                    }
                }
                scm {
                    connection.set("scm:git:https://github.com/invokation-games/kotlin-sdk.git")
                    developerConnection.set("scm:git:ssh://git@github.com/invokation-games/kotlin-sdk.git")
                    url.set("https://github.com/invokation-games/kotlin-sdk")
                }
            }
        }
    }
    repositories {
        maven {
            url = rootProject.layout.buildDirectory.dir('staging-deploy')
        }
    }
}
